buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'kr.entree.spigradle' version '2.2.4'
    id 'io.freefair.lombok' version '6.3.0'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 17
targetCompatibility = 17

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    // depends = ['']
    softDepends = ['Vault']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    spigot()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven {url = 'https://mvnrepository.com/artifact/com.google.code.gson/gson'}
}

dependencies {
    // using spigot-api
    implementation spigot(mcVersion)
    // or using paper-api
    // implementation "io.papermc.paper:paper-api:${mcVersion}-R0.1-SNAPSHOT"

    // Add your dependencies here
    // Here are some opinionated dependencies that might help you with your plugin development:

    implementation 'com.google.code.gson:gson:2.10'
    implementation 'org.luaj:luaj-jse:3.0.1'
}

shadowJar {
    dependencies {
        include(dependency('com.google.code.gson:gson:2.10'))
        include(dependency('org.luaj:luaj-jse:3.0.1'))
    }
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'